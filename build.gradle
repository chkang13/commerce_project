plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
}

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.my'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-web'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        // Spring cloud
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

        // DB (mysql)
        implementation 'org.springframework.boot:spring-boot-starter-jdbc'
        runtimeOnly 'com.mysql:mysql-connector-j'

        // JPA, Hibernate
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.hibernate:hibernate-validator-test-utils:5.4.3.Final'

        // Redis
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'

        // Spring Security
        implementation 'org.springframework.boot:spring-boot-starter-security'

        // JWT
        implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
        implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
        implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

        // Mail
        implementation 'org.springframework.boot:spring-boot-starter-mail:3.0.5'
        implementation 'javax.mail:javax.mail-api:1.6.2'
    }

    // Eureka Server를 위한 설정
    ext {
        set('springCloudVersion', "2023.0.0")
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

}

bootJar { enabled = false }
jar { enabled = true }

project(':member-service') {
    dependencies {
        implementation project(':core-service')
    }
}

project(':product-service') {
    dependencies {
        implementation project(':core-service')
    }
}

project(':order-service') {
    dependencies {
        implementation project(':core-service')
    }
}